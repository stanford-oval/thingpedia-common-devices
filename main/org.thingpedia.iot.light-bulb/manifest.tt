abstract class @org.thingpedia.iot.light-bulb extends @org.thingpedia.iot.switch
#_[thingpedia_name="Light Bulb"]
#_[thingpedia_description="The general interface supported by all light bulbs."]
#[license="BSD-3-Clause"]
#[license_gplcompatible=true]
#[subcategory="home"]
{
  monitorable query power(out power : Enum(on,off))
  #_[canonical=["the power state of the light bulb",
                "the power state of my lights",
                "whether the lights are on or off",
                "light state"]]
  #_[confirmation="the power state of ${__device}"]
  #[confirm=false]
  #_[result=["your lights are ${power}"]];

  action alert_long()
  #_[canonical=["loop the color in my light bulb",
                "start disco lights",
                "color loop the lights",
                "loop the color of the light bulb"]]
  #_[confirmation="flash alerts on ${__device}"]
  #[confirm=false];

  action color_loop()
  #_[canonical=["flash the lights",
                "alert me by flashing the lights",
                "flash the lights as alerts"]]
  #_[confirmation="loop colors on ${__device}"]
  #[confirm=false];

  action set_power(in req power: Enum(on,off)
                   #_[prompt="do you want to turn it on or off"]
                   #_[canonical="power"])
  #_[canonical=["set the power on the lights", "set the power on the lamp"]]
  #_[confirmation="turn $power ${__device}"]
  #[confirm=false];

  action set_color(in req color: Enum(white,black,red,orange,yellow,green,blue,purple,pink,brown,gray,chartreuse,cyan,azure,violet,magenta,rose,amber,teal,indigo)
                   #_[prompt="do you want to change color to the light"]
                   #_[canonical="color"])
  #_[canonical=["set the color on the lights", "set the color on the lamp"]]
  #_[confirmation="set ${__device} $color"]
  #[confirm=false];

  action set_brightness(in req pct: Number #_[prompt="from 0 to 100, which brightness do you want to set"]
                    #_[canonical={
                      base=["brightness", "value"],
                      preposition=["to %"]
                    }])
  #_[canonical=["change the brightness of the light", "set the brightness on the lamp"]]
  #_[confirmation="set ${__device} $pct percent"]
  #[min_number=0]
  #[max_number=100] 
  #[confirm=false];

  action raise_brightness()
  #_[canonical=["raise the light brightness", "increasing the brightness of the lamp"]]
  #_[confirmation="raise brightness on ${__device}"]
  #[confirm=false];

  action lower_brightness()
  #_[canonical=["lower the light brightness", "decrease the brightness of the lamp"]]
  #_[confirmation="lower brightness on ${__device}"]
  #[confirm=false];

}