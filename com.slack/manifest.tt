class @com.slack {
  import loader from @org.thingpedia.v2();
  import config from @org.thingpedia.config.oauth2();

  monitorable list query channel_history(out channel: Entity(tt:hashtag) #_[prompt="What channel are you interested in?"],
                                         out date: Date,
                                         out sender: Entity(tt:username) #_[prompt="What user are you interested in?"],
                                         out message: String)
  #_[canonical="get message on slack"]
  #_[confirmation="your messages on Slack"]
  #_[confirmation_remote="$__person's messages on Slack"]
  #_[formatted=[{type="text",text="New message on #${channel} from @${sender}: ${message}"}]]
  #[poll_interval=60000ms]
  #[doc="Polls Slack and sends you any new messages that appear in public channels of your Slack team."];

  action send(in req channel: Entity(tt:hashtag) #_[prompt="What channel do you want to send the message to?"],
              in req message: String #_[prompt="What message do you want me to send?"])
  #_[canonical="send message on slack"]
  #_[confirmation="send a message on slack to channel $channel saying $message"]
  #_[confirmation_remote="send a message on $__person's slack to channel $channel saying $message"]
  #[doc="Send a message to the specified Slack channel as a bot named after you. Omit the hashtag from the channel name."]
  #[label="send a message"];

  action updateChannelPurpose(in req channel: Entity(tt:hashtag) #_[prompt="What channel do you want to change?"],
                              in req purpose: String #_[prompt="What purpose do you want to set?"])
  #_[canonical="update channel purpose on slack"]
  #_[confirmation="update the purpose of slack channel $channel to $purpose"]
  #_[confirmation_remote="update the purpose of $__person's slack channel $channel to $purpose"]
  #[doc="Update the specified channel's purpose. Omit the hashtag from the channel name."]
  #[label="update the channel's purpose"];

  action updateChannelTopic(in req channel: Entity(tt:hashtag) #_[prompt="What channel do you want to change?"],
                            in req topic: String #_[prompt="What should be the new topic?"])
  #_[canonical="update channel topic on slack"]
  #_[confirmation="update the topic of slack channel $channel to $topic"]
  #_[confirmation_remote="update the topic of $__person's slack channel $channel to $topic"]
  #[doc="Update the specified channel's topic. Omit the hashtag from the channel name."]
  #[label="update the channel's topic"];

  action setPresence(in req presence: Enum(away,auto) #_[prompt="What should be your new presence?"])
  #_[canonical="set presence on slack"]
  #_[confirmation="set your presence status on slack to $presence"]
  #_[confirmation_remote="set $__person's presence status on slack to $presence"]
  #[doc="Update your displayed presence. Presence must be 'away' or 'auto'."]
  #[label="set your presence"];

  action send_picture(in req channel: Entity(tt:hashtag) #_[prompt="What channel do you want to send the message to?"],
                      in req caption: String #_[prompt="What is the caption of the picture?"],
                      in req picture_url: Entity(tt:picture) #_[prompt="Upload the picture now."])
  #_[canonical="send picture on slack"]
  #_[confirmation="send a picture on slack to channel $channel with caption $caption"]
  #_[confirmation_remote="send a picture on $__person's slack to channel $channel with caption $caption"]
  #[doc="Send a message with a picture attachment to the specified Slack channel. Omit the hashtag from the channel name."];
}

